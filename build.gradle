buildscript {
    ext {
        springBootVersion = '2.1.7.RELEASE'
        querydslPluginVersion = '1.0.10'
        nodePluginVersion='2.2.4'
    }
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:${querydslPluginVersion}")
        classpath("com.github.node-gradle:gradle-node-plugin:${nodePluginVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.ewerk.gradle.plugins.querydsl'
apply plugin: 'com.github.node-gradle.node'

group = 'com.vsnamta'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
    compileOnly('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.mariadb.jdbc:mariadb-java-client')
    compile('com.querydsl:querydsl-jpa')
    compileOnly('com.querydsl:querydsl-apt')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
    compile('org.flywaydb:flyway-core')

	testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('com.h2database:h2')
    testCompile("org.springframework.security:spring-security-test")
}

def querydslSrcDir = 'src/main/generated'

querydsl {
    library = 'com.querydsl:querydsl-apt'
    jpa = true
    querydslSourcesDir = querydslSrcDir
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', querydslSrcDir]
        }
    }
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

configurations { 
    querydsl.extendsFrom compileClasspath
}

node {
    download = true
    version = '14.16.1'
    npmVersion = ''
    distBaseUrl = 'https://nodejs.org/dist'
    workDir = file("${project.projectDir}/frontend/nodejs")
    nodeModulesDir = file("${project.projectDir}/frontend") 
}

task buildFrontend(type: NpmTask) {
    args = ['run', 'build']
}

task testFrontend(type: NpmTask) {
    args = ['run', 'test']
}

task copyFrontend(type: Copy) {
    from 'frontend/dist/.'
    into 'build/resources/main/static/.'
}

buildFrontend.dependsOn npmInstall
testFrontend.dependsOn buildFrontend
copyFrontend.dependsOn testFrontend
processResources.dependsOn copyFrontend